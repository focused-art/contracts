#import "../../types.jsligo" "T"
#import "../../views.jsligo" "V"
#import "../../helpers.jsligo" "H"
#import "../../../common/utils/address.jsligo" "A"

// Update token royalties
export const entrypoint = (
  input: T.update_royalties_params,
  permit_opt: option<T.Permit.permit>,
  s1: T.storage
): T.@return => {
  let s = s1;

  const sender : address = match(permit_opt) {
    when(Some(permit)): do {
      const param_bytes : bytes = Bytes.pack(["%update_royalties", input]);
      s = H.check_and_store_permit(permit, param_bytes, s);
      return A.address_from_key(permit.signer);
    };
    when(None()): Tezos.get_sender();
  };

  assert_with_error(
    V.has_role([sender, Royalties_manager()], s),
    "FA2_INVALID_ROYALTIES_MANAGER_ACCESS"
  );

  // Initialize operations
  let operations: list<operation> = list([]);

  for (const generated of input) {
    const [token_id, royalties] = generated;
    s.royalties = Big_map.update(token_id, Some(royalties), s.royalties);

    // Events
    const token_royalties_update_event: T.token_royalties_update_event =
      { token_id: token_id; new_royalties: Some(royalties) };

    operations = list([
      Tezos.emit("%token_royalties_update", token_royalties_update_event),
      ...operations
    ]);
  };

  // Send hooks
  for (const hook of V.get_hooks(Royalties_hook(), s)) {
    operations = list([
      Tezos.transaction(input, 0mutez, H.get_update_royalties_hook(hook)),
      ...operations
    ]);
  };

  return [operations, s]
};
