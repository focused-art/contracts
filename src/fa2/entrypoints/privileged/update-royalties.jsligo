// Update token royalties
const update_royalties = (input: update_royalties_params, permit_opt: option<permit>, s1: storage): @return => {
  let s = s1;

  const sender : address = match(permit_opt, {
    Some: permit => {
      const param_bytes : bytes = Bytes.pack(["%update_royalties", input]);
      s = check_and_store_permit(permit, param_bytes, s);
      return address_from_key(permit.signer);
    },
    None: () => Tezos.get_sender()
  });

  assert_with_error(
    has_role([sender, Royalties_manager()], s),
    "FA2_INVALID_ROYALTIES_MANAGER_ACCESS"
  );

  // Initialize operations
  let operations: list<operation> = list([]);

  for (const generated of input) {
    const [token_id, royalties] = generated;
    s.royalties = Big_map.update(token_id, Some(royalties), s.royalties);

    // Events
    const token_royalties_update_event: token_royalties_update_event =
      { token_id: token_id; new_royalties: Some(royalties) };

    operations = list([
      Tezos.emit("%token_royalties_update", token_royalties_update_event),
      ...operations
    ]);
  };

  // Send hooks
  for (const hook of get_hooks(Royalties_hook(), s)) {
    operations = list([
      Tezos.transaction(input, (0 as mutez), get_update_royalties_hook(hook)),
      ...operations
    ]);
  };

  return [operations, s]
};
